@rem Converts line breaks to DOS CR/LF format and configures JPGalleg to be
@rem built under specified platform. Derived from the Allegro version of this
@rem same script.

@echo off

rem Test if there are too many args.
if [%3] == []        goto arg2
goto help

:arg2
rem Test if second arg is ok.
if [%2] == [--quick] goto arg1
if [%2] == [--utod]  goto arg1
if [%2] == []        goto arg1
goto help

:arg1
rem Test if first arg is ok.
if [%1] == [djgpp]   goto head
if [%1] == [mingw32] goto head
if [%1] == [mingw]   goto head
if [%1] == [msvc]    goto head
if [%1] == [msvc6]   goto head
if [%1] == [msvc7]   goto head
if [%1] == [msvc8]   goto head
if [%1] == [msvc9]   goto head
goto nosup

:head
rem Generate header of makefile
echo # generated by fix.bat > makefile

if [%1] == [djgpp]   goto djgpp
if [%1] == [mingw32] goto mingw32
if [%1] == [mingw]   goto mingw32
if [%1] == [msvc]    goto msvc6
if [%1] == [msvc6]   goto msvc6
if [%1] == [msvc7]   goto msvc7
if [%1] == [msvc8]   goto msvc8
if [%1] == [msvc9]   goto msvc9

echo fix.bat internal error: not reached
goto help

:djgpp
echo Configuring JPGalleg for DOS/djgpp...
echo MAKEFILE_INC = makefile.dj >> makefile
goto tail

:mingw32
echo Configuring JPGalleg for Windows/Mingw32...
echo MAKEFILE_INC = makefile.mgw >> makefile
goto tail

:msvc6
echo Configuring JPGalleg for Windows/MSVC6...
echo MAKEFILE_INC = makefile.vc >> makefile
goto tail

:msvc7
echo Configuring JPGalleg for Windows/MSVC7...
echo COMPILER_MSVC7 = 1 >> makefile
echo MAKEFILE_INC = makefile.vc >> makefile
goto tail

:msvc8
echo Configuring JPGalleg for Windows/MSVC8...
echo COMPILER_MSVC8 = 1 >> makefile
echo MAKEFILE_INC = makefile.vc >> makefile
goto tail

:msvc9
echo Configuring JPGalleg for Windows/MSVC9...
rem MSVC9 uses MSVC8 settings
echo COMPILER_MSVC8 = 1 >> makefile
echo MAKEFILE_INC = makefile.vc >> makefile
goto tail

:help
echo.
echo Usage: fix platform [--quick,--utod]
echo.
echo Where platform is one of: djgpp, mingw32, msvc6, msvc7, msvc8, msvc9.
echo The --quick parameter is used to turn off LF to CR/LF conversion.
echo The --utod parameter converts from Unix to DOS/Win32 format.
echo.
goto end

:nosup
echo Platform not supported by JPGalleg.
goto end

:tail
rem Generate last line of makefile and optionally convert CR/LF.
echo include makefile.all >> makefile

if [%2] == [--utod] goto conv
goto end

:conv
echo Converting JPGalleg files to DOS CR/LF format...
utod .../*.bat .../*.sh .../*.c .../*.s .../*.h .../*.txt
utod .../*.inc .../*.scm .../*.scr .../*.scu makefile.*

:end
