Import("context")
Import("env")
import addons, helpers
import os

def getArgumentOption(name, default):
    arg = ARGUMENTS.get(name,default)
    if arg == "yes" or arg == "1":
        return 1
    return 0

myEnv = env.Clone()

tests = {}

def setupPlatform(settings, config):
    settings["ALLEGRO_CFG_IIO_HAVE_PNG"] = config.CheckHeader("png.h") and\
        config.CheckLib("png")
    settings["ALLEGRO_CFG_IIO_HAVE_JPG"] = config.CheckHeader(["stdio.h",
        "stdlib.h", "jpeglib.h"]) and config.CheckLib("jpeg")
    return config.Finish()

settings, configure_env = helpers.do_configure('iio', context,
    tests, setupPlatform,
    'allegro5/internal/aintern_iio_cfg.h.cmake',
    'allegro5/internal/aintern_iio_cfg.h',
    getArgumentOption('config', 0))

source = ["bmp.c", "iio.c", "pcx.c", "tga.c"]
libs = []

if settings["ALLEGRO_CFG_IIO_HAVE_PNG"]:
    env.Append(LIBS = ['png','z'])
    source.append('png.c')
    libs.extend(['png','z'])

if settings["ALLEGRO_CFG_IIO_HAVE_JPG"]:
    env.Append(LIBS = ['jpeg'])
    source.append('jpg.c')
    libs.append('jpeg')

result = addons.do_build(
    context = context,
    env = myEnv,
    source = source,
    libs = libs,
    dir = "iio",
    name = "a5_iio",
    install_headers = ["allegro5/a5_iio.h",
    "allegro5/internal/aintern_iio_cfg.h"]
)

Return("result")
