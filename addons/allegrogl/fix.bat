@echo off
echo.

set MAKE_PROJECT=AllegroGL
echo # generated by fix.bat > makefile

if [%1] == [djgpp]   goto djgpp
if [%1] == [dmc]     goto dmc
if [%1] == [mingw]   goto mingw32
if [%1] == [mingw32] goto mingw32
if [%1] == [msvc]    goto msvc
if [%1] == [msvc6]   goto msvc6
if [%1] == [msvc7]   goto msvc7
if [%1] == [msvc8]   goto msvc8
if [%1] == [msvc9]   goto msvc9

if [%1] == [help]    goto help
if [%1] == [--help]  goto help
if [%1] == [-?]      goto help
if [%1] == []        goto help

echo Platform not supported by AllegroGL.
goto end

:djgpp
set MAKE_COMPILER=DJGPP
set MAKEFILE=makefile.dj
goto fix

:dmc
set MAKE_COMPILER=dmc
set MAKEFILE=makefile.dmc
goto fix

:mingw32
set MAKE_COMPILER=Mingw32
set MAKEFILE=makefile.mgw
goto fix

:msvc
echo.
echo Notice: Because no version was specified, MSVC 6 has been chosen. 
echo.
echo If you are using a newer version, you should use 'msvc7', 'msvc8'
echo or 'msvc9' instead.
echo msvc7 should be used for MSVC .NET or MSVC .NET 2003
echo msvc8 should be used for MSVC .NET 2005
echo msvc9 should be used for MSVC .NET 2008
echo.

:msvc6
set MAKE_COMPILER=MSVC6
echo COMPILER_MSVC6 = 1 >> makefile
goto :msvccommon

:msvc7
set MAKE_COMPILER=MSVC7
echo COMPILER_MSVC7 = 1 >> makefile
goto :msvccommon

:msvc8
set MAKE_COMPILER=MSVC8
echo COMPILER_MSVC8 = 1 >> makefile
goto :msvccommon

:msvc9
set MAKE_COMPILER=MSVC9
rem MSVC9 uses MSVC8 settings
echo COMPILER_MSVC8 = 1 >> makefile
goto :msvccommon

:msvccommon
set MAKEFILE=makefile.vc
echo include ../../makefile.helper >> makefile
goto fix

:fix

echo Configuring %MAKE_PROJECT% for %MAKE_COMPILER%...
echo include make/%MAKEFILE% >> makefile

:done

set MAKE_PROJECT=
set MAKEFILE=
set MAKE_COMPILER=

goto end

:help

echo Compilation target adjustment.
echo   Usage: fix [platform]
echo.
echo   [platform] is one of: djgpp, dmc, mingw32, msvc6, msvc7, msvc8, msvc9
echo   Example: fix mingw32
echo.
echo   For the Unix build, please use ./fix.sh
echo.

:end


